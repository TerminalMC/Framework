buildscript {
    dependencies.add("classpath", "org.spongepowered:vanillagradle:${vanillagradle_version}")
}

plugins {
    id("multiloader-common")
    id("org.spongepowered.gradle.vanilla")
}

dependencies {
    compileOnly("org.spongepowered:mixin:${mixin_version}")
    compileOnly("io.github.llamalad7:mixinextras-common:${mixinextras_version}")
    annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}")

    // Mod dependencies use Forge version to avoid remapping complications
    implementation("me.shedaniel.cloth:cloth-config-forge:${clothconfig_version_forge}")
    implementation("maven.modrinth:1eAoo2KR:${yacl_version_forge}")
}

minecraft {
    version(minecraft_version)
    if (file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
